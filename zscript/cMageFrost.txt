class cFrostShards : MWeapFrost replaces MWeapFrost {
	const SPREAD_X = 1.8;
	const SPREAD_Y = 0.4;
	
	Default {
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 25;
		Weapon.YAdjust 18;
		Weapon.BobStyle "Smooth";
		Weapon.BobRangeX 1.0;
		Weapon.BobRangeY 0.7;
		Weapon.BobSpeed 1.4;
		+WEAPON.NOAUTOAIM
		+WEAPON.NOALERT
		+INVENTORY.RESTRICTABSOLUTELY
	}
	
	States {
	Spawn:
		WMCS A 7 Bright Light("FrostShardsWeapon1");
		WMCS B 7 Bright Light("FrostShardsWeapon2");
		WMCS C 7 Bright Light("FrostShardsWeapon3");
		WMCS B 7 Bright Light("FrostShardsWeapon2");
		Loop;
	Select:
		TNT1 A 0 A_SetCrosshair(59);
		CONE A 1 A_Raise(15);
		Wait;
	Deselect:
		CONE A 1 A_Lower(15);
		Wait;
	Fire:
		CONE B 4 { invoker.special1 = 0; }
		CONE C 2;
	Hold:
		CONE C 2 A_PlaySound("IceGuyAttack", CHAN_WEAPON, 0.6);
		CONE D 2;
		CONE E 1;
		CONE E 2 FrostShardAttack();
		CONE D 1;
		TNT1 A 0 CheckContinueFire();
		CONE C 2 A_ReFire();
		CONE F 3;
		CONE G 4;
		Goto Ready;
	Cooldown:
		CONE F 7 A_PlaySound("*pain", CHAN_VOICE);
		CONE G 5 A_ClearReFire();
		CONE A 5;
		Goto Ready;
	}
	
	action void FrostShardAttack() {
		Vector2 spread = (SPREAD_X*frandom[FrostShardSpread](-1,1), SPREAD_Y*frandom[FrostShardSpread](-1,1));
		A_FireProjectile("cFrostShardMissile", spread.x, pitch: spread.y-.3);
		
		A_AlertMonsters(1500);
		invoker.special1++;
	}
	
	action state CheckContinueFire() {
		if (invoker.special1 > 15) {
			if (random() < invoker.special1) {
				invoker.special1 = 0;
				return invoker.ResolveState("Cooldown");
			}
		}
		return null;
	}
}

class cFrostShardMissile : Actor {
	Default {
		Radius 11;
		Height 8;
		Projectile;
		Speed 45;
		DamageFunction int(21 * frandom[FrostShardDamage](0.9,1.1));
		DamageType "Ice";
		DeathSound "MageShardsExplode";
		Obituary "$OB_MPMWEAPFROST";
	}
	
	States {
	Spawn:
		SHRD ABC 4 Bright Light("FrostShard1");
		Loop;
	Death:
		TNT1 A 0 Spawn("cFrostShotPuff", pos);
		Stop;
	}
}

class cFrostShotPuff : Actor {
	Default {
		RenderStyle "Translucent";
		Alpha 0.8;
		+NOINTERACTION
		+NOBLOCKMAP
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		SHEX AB 3 Light("FrostShard1");
		SHEX CDE 4 Light("FrostShard2");
		Stop;
	}
}
