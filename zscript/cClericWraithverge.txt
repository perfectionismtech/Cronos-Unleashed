class cWraithverge : cWeapon replaces CWeapWraithverge {
	Default {
		Inventory.RestrictedTo "cClericPlayer";
		Health 3; // Weapon Pieces
		+INVENTORY.NOATTENPICKUPSOUND
		Weapon.AmmoType1 "Mana1";
		Weapon.AmmoType2 "Mana2";
		Weapon.AmmoUse1 18;
		Weapon.AmmoUse2 18;
		Weapon.AmmoGive1 25;
		Weapon.AmmoGive2 25;
		+WEAPON.PRIMARY_USES_BOTH;
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 1.6;
		Weapon.BobRangeX 0.6;
		Weapon.BobRangeY 0.8;
		Weapon.SelectionOrder 3000;
		Inventory.PickupMessage "$TXT_WEAPON_C4";
		Inventory.PickupSound "WeaponBuild";
		Tag "$TAG_CWEAPWRAITHVERGE";

	}
	
	States {
	Spawn:
		TNT1 A -1;
		Stop;
	Select:
		TNT1 A 0 A_SetCrosshair(57);
		CHLY A 1 A_Raise(SELECT_SPEED);
		Wait;
	Deselect:
		CHLY A 1 A_Lower(SELECT_SPEED);
		Wait;
	Ready:
		CHLY A 1 A_WeaponReady();
		Loop;
	Fire:
		CHLY AB 2 Bright Offset(0, 32);
		CHLY C 2 Bright Offset(0, 34);
		CHLY D 3 Bright Offset(0, 36);
		CHLY E 3 Bright Offset(0, 38);
		CHLY F 6 Bright Offset(0, 48) WraithvergeAttack();
		CHLY A 4 Bright Offset(0, 40);
		CHLY A 5 Offset(0, 36);
		CHLY A 9 Offset(0, 32);
		Goto Ready;
	}
	
	action void WraithvergeAttack() {
		A_FireProjectile("cWraithvergeMissile", 0, true, 0, -4);
		A_SetBlend("58 58 58", 0.7, 20);
		A_PlaySound("HolySymbolFire", CHAN_WEAPON);
		A_AlertMonsters(ALERT_DIST_NORM);
	}
}

class cWraithvergeMissile : HolyMissile replaces HolyMissile {
	States {
	Spawn:
		SPIR P 2 Bright;
		SPIR PPPPP 3 Bright A_SpawnItemEx("HolyMissilePuff");
		SPIR P 1 Bright;
	Death:
		SPIR P 1 Bright A_CHolyAttack2();
		Stop;
	}
}

class cWraithvergeSmoke : HolyMissilePuff replaces HolyMissilePuff {
	Default {
		Alpha 0.4;
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		SPIR QRSTU 3 A_FadeOut(0.05, FTF_CLAMP);
		Stop;
	}
}

class cWraithvergePiece1 : CWeaponPiece1 replaces CWeaponPiece1 {
	Default {
		WeaponPiece.Weapon "cWraithverge";
		-FLOATBOB
		+INVENTORY.RESTRICTABSOLUTELY
	}
	
	States {
	Spawn:
		WCH1 A -1 Bright Light("WraithvergePiece");
		Stop;
	}
}

class cWraithvergePiece2 : CWeaponPiece2 replaces CWeaponPiece2 {
	Default {
		WeaponPiece.Weapon "cWraithverge";
		-FLOATBOB
		+INVENTORY.RESTRICTABSOLUTELY
	}
	
	States {
	Spawn:
		WCH2 A -1 Bright Light("WraithvergePiece");
		Stop;
	}
}

class cWraithvergePiece3 : CWeaponPiece3 replaces CWeaponPiece3 {
	Default {
		WeaponPiece.Weapon "cWraithverge";
		-FLOATBOB
		+INVENTORY.RESTRICTABSOLUTELY
	}
	States {
	Spawn:
		WCH3 A -1 Bright Light("WraithvergePiece");
		Stop;
	}
}
