class cArcOfDeath : cWeapon replaces MWeapLightning {
	Default {
		Inventory.RestrictedTo "cMagePlayer";
		Weapon.AmmoType1 "Mana2";
		Weapon.AmmoUse1 6;
		Weapon.AmmoGive1 25;
		Weapon.YAdjust 4;
		Weapon.BobStyle "Smooth";
		Weapon.BobSpeed 1.4;
		Weapon.BobRangeX 1.0;
		Weapon.BobRangeY 0.7;
		+NOGRAVITY
		Weapon.SelectionOrder 1100;
		Inventory.PickupMessage "$TXT_WEAPON_M3";
		Tag "$TAG_MWEAPLIGHTNING";
	}
	
	States {
	Spawn:
		WMLG HGFEDCBA 5 Bright Light("ArcOfDeathWeapon");
		Loop;
	Select:
		TNT1 A 0 A_SetCrosshair(60);
		TNT1 A 0 SpawnWeaponLight('cArcOfDeathLight');
		MLNG A 1 Bright A_Raise(SELECT_SPEED);
		Wait;
	Deselect:
		MLNG A 1 Bright A_Lower(SELECT_SPEED);
		Wait;
	Ready:
		MLNG AAAAAAA 1 Bright A_WeaponReady();
		TNT1 A 0 PlayReadySound();
		MLNG BBBBBB 1 Bright A_WeaponReady();
		MLNG CCCCCCC 1 Bright A_WeaponReady();
		TNT1 A 0 PlayReadySound();
		MLNG BBBBBB 1 Bright A_WeaponReady();
		Loop;
	Fire:
		MLNG D 4 Bright;
		MLNG E 4 Bright;
		MLNG F 4 Bright ArcOfDeathFire();
		MLNG G 4 Bright;
		MLNG H 3 Bright;
		MLNG I 3 Bright;
		TNT1 A 11;
		Goto WindDown;
	WindDown:
		MLNG C 2 Bright Offset(0, 55);
		MLNG B 2 Bright Offset(0, 50);
		MLNG B 2 Bright Offset(0, 45);
		MLNG B 2 Bright Offset(0, 40);
		Goto Ready;
	}
	action void ArcOfDeathFire() {
		if (player == null) return;
		
		Weapon weap = player.readyWeapon;
		if (weap != null) weap.DepleteAmmo(weap.bAltFire);
		
		LightningFloor fmo = LightningFloor(SpawnPlayerMissile("LightningFloor"));
		LightningCeiling cmo = LightningCeiling(SpawnPlayerMissile("LightningCeiling"));
		if (fmo) {
			fmo.special1 = 0;
			fmo.lastenemy = cmo;
			fmo.A_LightningZap();
		}
		if (cmo) {
			cmo.tracer = NULL;
			cmo.lastenemy = fmo;
			cmo.A_LightningZap();
		}
		
		A_PlaySound("MageLightningFire", CHAN_WEAPON);
		A_AlertMonsters(ALERT_DIST_NORM);
	}
	
	action void PlayReadySound() {
		if (random() < 144) {
			A_PlaySound("MageLightningReady", CHAN_WEAPON);
		}
	}
}

class cArcOfDeathLight : cPlayerLight {
	Default {
		Args 26, 26, 77, 40, 32;
		DynamicLight.Type "Flicker";
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		specialf1 = 340;
	}
	
	override void Tick() {
		Super.Tick();
		
		if(!target || !target.player || !target.player.readyWeapon ||
			target.player.readyWeapon.GetClass() != "cArcOfDeath") {
			Destroy();
		}
	}
}

