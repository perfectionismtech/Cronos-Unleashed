class cSerpentStaff : CWeapStaff replaces CWeapStaff {
	const DRAIN_RANGE = 80;
	const DRAIN_DAMAGE = 37;
	const DRAIN_RATIO = 0.4;
	
	Default {
		Weapon.AmmoUse1 2;
		Weapon.AmmoGive1 25;
		Weapon.YAdjust 8;
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobRangeX 0.6;
		Weapon.BobRangeY 0.8;
		Weapon.BobSpeed 1.6;
		+WEAPON.NOAUTOAIM
		+WEAPON.NOALERT
		+INVENTORY.RESTRICTABSOLUTELY
	}
	
	States {
	Select:
		TNT1 A 0 A_SetCrosshair(55);
		CSSF C 1 A_Raise(15);
		Loop;
	Deselect:
		CSSF BB 1 A_Lower(15);
		CSSF C 1 A_Lower(15);
		Wait;
	Fire:
		TNT1 A 0 TryDrain();
		CSSF A 1 Offset(0, 43);
		CSSF J 3 Offset(0, 47) SerpentStaffFire();
		CSSF J 2 Offset(0, 41);
		CSSF A 1 Offset(0, 40);
		CSSF A 2 Offset(0, 39);
		CSSF A 2 Offset(0, 36);
		CSSF A 2 Offset(0, 34);
		Goto Ready+2;
	Fire.Drain:
		CSSF K 6 Offset(0, 36) A_PlaySound("ClericCStaffHitThing", CHAN_WEAPON);
		CSSF K 4 Offset(0, 38);
		CSSF K 3 Offset(0, 40);
		Goto Ready+2;
	}
	
	action state TryDrain() {
		if (player == null) return null;
		Weapon weapon = player.readyWeapon;
		
		FTranslatedLineTarget t;
		for (int i=0; i<=6; i=(i>0 ? -i : -i+2) ) {
			if (LineAttack(angle+i, DRAIN_RANGE, pitch, DRAIN_DAMAGE, "Melee", "cSerpentDrainPuff", 0, t)) {
				if (t.linetarget != null) {
					if (t.linetarget.bISMONSTER || t.linetarget.player) {
						if (!t.linetarget.bDORMANT && !t.linetarget.bINVULNERABLE) {
							if (t.linetarget.player && (!t.linetarget.IsTeammate(self) || level.teamDamage != 0)) {
								return null;
							}
							
							if (weapon != null && !weapon.DepleteAmmo(weapon.bAltFire)) return null;
							
							GiveBody(int(floor(DRAIN_DAMAGE * DRAIN_RATIO)));
							
							A_AlertMonsters(1500);
							if(GetCVar("weapon_shake")) A_Quake(1, 10, 0, 1, "");
							
							return ResolveState("Fire.Drain");
						}
					}
					
				}
				return null;
			}
		}
		
		return null;
	}
	
	action void SerpentStaffFire() {
		if (player == null) return;
		Weapon weapon = player.ReadyWeapon;
		
		if (weapon != null && !weapon.DepleteAmmo(weapon.bAltFire)) return;
		
		Actor mo;
		mo = A_FireProjectile("cSerpentStaffMissile", 1.4, false, -6);
		if (mo) mo.bSPRITEFLIP = true;
		
		mo = A_FireProjectile("cSerpentStaffMissile", -1.4, false, 6);
		if (mo) mo.WeaveIndexXY = 32;
		
		A_AlertMonsters(1500);
		A_PlaySound ("ClericCStaffFire", CHAN_WEAPON);
	}
}

class cSerpentStaffMissile : CStaffMissile replaces CStaffMissile {
	Default {
		DamageFunction int(26 * frandom[SSDamage](0.9,1.1));
		Speed 22;
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		CSSF DDEE 1 Bright Light("SerpentStaffMissile") A_Weave(3, 0, 0.7, 0);
		Loop;
	Death:
		TNT1 A 0 SpawnParticles();
		CSSF FG 4 Bright Light("SerpentStaffBlast1") A_StopSound(CHAN_BODY);
		CSSF HI 3 Bright Light("SerpentStaffBlast2");
		Stop;
	}
	
	void SpawnParticles() {
		for (int i = 0; i < 8; i++) {
			A_SpawnParticle("27EC30", SPF_FULLBRIGHT, 30, 3, 0, 0, 0, 0, 
				frandom(-2, 2), frandom(-2, 2), frandom(1, 4), 0, 0, -0.4, 1, 0, -0.07);
		}
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		bNOBLOOD = (random() < 128);
		A_PlaySound("SerpentFXContinuous", CHAN_BODY, 0.4, true);
	}
}

class cSerpentDrainPuff : cPuff {
	Default {
		RenderStyle "Translucent";
		Alpha 0.6;
		VSpeed 0.7;
	}
	
	States {
	Spawn:
		CSSF LMNO 5 Bright A_FadeOut(0.1);
		Stop;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		if (!target) Destroy();
	}
}
