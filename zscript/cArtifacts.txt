class cCrystalVial : CrystalVial replaces CrystalVial {
	Default {
		Radius 12;
		Height 32;
		+INVENTORY.NOSCREENFLASH
	}
	
	States {
	Spawn:
		PTN1 AAABCD 9 Light("CrystalVialItem") DoSpawnParticle();
		Loop;
	}
	
	void DoSpawnParticle() {
		A_SpawnParticle("0055FF", 0, 70, 2, 0, 
			frandom(-4,4), frandom(-4,4), 30, 
			frandom(-0.1,0.1), frandom(-0.1,0.1), 0, 
			0, 0, 0.015);
	}
	
	override bool TryPickup(in out Actor other) {
		bool pickup = Super.TryPickup(other);
		if (pickup) {
			if (other.GetCVar("healing_flash")) {
				other.A_SetBlend("0D78DB", 0.2, 20);
			} else {
				// Play the regular flash
				other.player.bonuscount = BONUSADD;
			}
		}
		return pickup;
	}
}

class cQuartzFlask : ArtiHealth replaces ArtiHealth {
	Default {
		Radius 20;
		Height 32;
		-INVENTORY.PICKUPFLASH
		-INVENTORY.FANCYPICKUPSOUND
		-INVENTORY.INVBAR
		-COUNTITEM
		Inventory.PickupSound "misc/i_pkup";
	}
	
	States {
	Spawn:
		PTN2 A 9 Light("QuartzFlaskItem") DoSpawnParticle();
		Loop;
	}
	
	void DoSpawnParticle() {
		A_SpawnParticle("BE34E8", 0, 70, 3, 0, 
			frandom(-8, 8), frandom(-8, 8), 26, 
			frandom(-.1, .1), frandom(-.1, .1), 0, 
			0, 0, 0.01);
	}
	
	override bool Use(bool pickup) {
		if (owner.Health <= 0 || owner.Health >= PlayerPawn(owner).GetMaxHealth()) {
			return false;
		}
		if (owner.CheckInventory("cQuartzRegen", 1)) {
			return false;
		}
		
		owner.A_GiveInventory("cQuartzRegen");
		if (owner.GetCVar("healing_flash")) owner.A_SetBlend("D266EC", 0.3, 20);
		return true;
	}
}

class cQuartzRegen : Powerup {
	Default {
		Powerup.Duration 50;
	}
	
	override void DoEffect() {
		Super.DoEffect();
		if (owner != null && owner.health > 0 && (level.time & 1) == 0) {
			owner.GiveBody(1);
		}
		if (owner.Health >= PlayerPawn(owner).GetMaxHealth()) {
			Destroy();
		}
	}
}

class cRepulsionDisc : ArtiBlastRadius replaces ArtiBlastRadius {
	Default {
		Radius 20;
		Height 32;
		-INVENTORY.PICKUPFLASH
		-INVENTORY.FANCYPICKUPSOUND
		-INVENTORY.INVBAR
		-COUNTITEM
		Inventory.PickupSound "misc/i_pkup";
	}
	
	States {
	Spawn:
		BLST ABCDEFGH 4 Bright Light("RepulsionItem");
		Loop;
	Use:
		TNT1 A 0 A_Blast(BF_DONTWARN, 255, 180.0, 12.0);
	}
}

class cMysticUrn : Inventory replaces ArtiSuperHealth {
	Default {
		Radius 20;
		Height 32;
		+NOGRAVITY
		+FLOATBOB
		+COUNTITEM
		+INVENTORY.INVBAR;
		+INVENTORY.FANCYPICKUPSOUND;
		Inventory.DefMaxAmount;
		Inventory.Icon "ARTISPHL";
		Inventory.PickupFlash "PickupFlash";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "$TXT_ARTISUPERHEALTH";
		Tag "$TAG_ARTISUPERHEALTH";
	}
	
	States {
	Spawn:
		SPHL A -1;
		Stop;
	}
	
	override bool Use(bool pickup) {
		if (owner == null) return false;
		
		if (owner.health <= 100) {
			owner.GiveBody(100, 200);
			if (owner.GetCVar("healing_flash")) owner.A_SetBlend("FFF39D", 0.5, 20);
			return true;
		} else {
			return false;
		}
	}
}

class cIconDefender: ArtiInvulnerability2 replaces ArtiInvulnerability2 {
	Default {
		Radius 20;
		Height 32;
	}
	
	States {
	Spawn:
		DEFN ABCD 4 Bright Light("IconItem");
		Loop;
	}
}

class cDragonskinBracers : ArtiBoostArmor replaces ArtiBoostArmor {
	Default {
		Radius 20;
		Height 32;
	}
	
	States {
	Spawn:
		BRAC ABCDEFGH 6 Bright Light("BracerItem");
		Loop;
	}
}

class cTorch : ArtiTorch replaces ArtiTorch {
	Default {
		Radius 20;
		Height 32;
	}
	
	States {
	Spawn:
		TRCH ABC 3 Bright Light("TorchItem");
		Loop;
	}
	
	override bool Use(bool pickup) {
		if (!GetCVar("classic_torch")) {
			PowerupType = "cPowerTorch";
		} else {
			PowerupType = "PowerTorch";
		}
		
		return Super.Use(pickup);
	}
}

class cPowerTorch : Powerup {
	cTorchLight light;
	
	Default {
		Powerup.Duration -120;
	}
	
	override void InitEffect() {
		Super.InitEffect();
		
		if (owner) owner.GiveInventoryType("cTorchCancel");
	}
	
	override void DoEffect() {
		Super.DoEffect();
		
		if (!light) {
			light = cTorchLight(Spawn("cTorchLight"));
			//light.target = owner; https://forum.zdoom.org/viewtopic.php?t=59923
			light.master = owner;
		}
		
		light.bDORMANT = isBlinking();
	}
	
	override void EndEffect() {
		Super.EndEffect();
		
		if (light) light.Destroy();
		if (owner) owner.TakeInventory("cTorchCancel", 1);
	}
}

class cTorchCancel : Inventory {
	Default {
		Inventory.Icon "ARTITRCX";
		Tag "Cancel Torch";
		+INVENTORY.INVBAR
		+INVENTORY.UNTOSSABLE
	}
	
	override bool Use(bool pickup) {
		owner.TakeInventory("cPowerTorch", 1); // This also calls EndEffect
		return true;
	}
}

class cTorchLight : cPlayerLight {
	Default {
		Args 120, 105, 80, 300, 390;
		DynamicLight.Type "RandomFlicker";
		+DYNAMICLIGHT.DONTLIGHTSELF
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		specialf1 = 3;
	}
}

class cWingsOfWrath : ArtiFly replaces ArtiFly {
	Default {
		Radius 20;
		Height 32;
		Inventory.MaxAmount 0;
		+INVENTORY.AUTOACTIVATE
		-COUNTITEM
	}
}

class cMana1 : Mana1 replaces Mana1 {
	Default {
		Radius 20;
		Height 32;
	}
	
	States {
	Spawn:
		MAN1 ABCDEFGHI 4 Bright Light("BlueMana");
		Loop;
	}
}

class cMana2 : Mana2 replaces Mana2 {
	Default {
		Radius 20;
		Height 32;
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		MAN2 ABCDEFGHIJKLMNOP 4 Bright Light("GreenMana");
		Loop;
	}
}

class cMana3 : Mana3 replaces Mana3 {
	Default {
		Radius 20;
		Height 32;
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		MAN3 ABCDEFGHIJKLMNOP 4 Bright Light("RedMana");
		Loop;
	}
}

class cMana4 : Inventory {
	Default {
		Radius 20;
		Height 32;
		+FORCEXYBILLBOARD
		+COUNTITEM
		+FLOATBOB
		+INVENTORY.INVBAR
		+INVENTORY.PICKUPFLASH
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.DefMaxAmount;
		Inventory.Icon "ARTIMAN4";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "Purple Mana";
		Tag "Purple Mana";
	}
	
	States {
	Spawn:
		MAN4 ABCDEFGHIJKLMNOP 4 Bright Light("PurpleMana");
		Loop;
	}
	
	override bool Use (bool pickup) {
		return false;
	}
}
