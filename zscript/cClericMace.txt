class cMace : CWeapon {
	Default {
		Inventory.RestrictedTo "cClericPlayer";
		+WEAPON.AXEBLOOD
		Weapon.YAdjust -8;
		Weapon.BobStyle "Smooth";
		Weapon.BobSpeed 1.4;
		Weapon.BobRangeX 0.8;
		Weapon.BobRangeY 1.0;
		Weapon.SelectionOrder 3500;
		Obituary "$OB_MPCWEAPMACE";
		Tag "$TAG_CWEAPMACE";
	}
	
	const MACE_DAMAGE = 27;
	
	States {
	Spawn:
		WMCE A -1;
		Stop;
	Select:
		TNT1 A 0 A_SetCrosshair(54);
		CMCE A 1 A_Raise(SELECT_SPEED);
		Wait;
	Deselect:
		CMCE A 1 A_Lower(SELECT_SPEED);
		Wait;
	Ready:
		CMCE A 1 A_WeaponReady();
		Loop;
	Fire:
		CMCE B 2 Offset(17, 41);
		CMCE B 2 Offset(4, 37);
		CMCE B 2 Offset(-12, 44);
		CMCE C 1 Offset(8, 44);
		CMCE D 1 Offset(23, 44) WeaponShake(1, 5);
		CMCE D 1 Offset(5, 60);
		CMCE E 1 Offset(-6, 76) MaceAttack();
		CMCE F 1 Offset(17, 49);
		CMCE F 1 Offset(1, 76);
		CMCE F 1 Offset(-13, 105);
		TNT1 A 11;
		TNT1 A 0 A_ReFire();
		Goto WindDown;
	WindDown:
		CMCE A 1 Offset(15, 78);
		CMCE A 1 Offset(15, 71);
		CMCE A 1 Offset(14, 64);
		CMCE A 1 Offset(13, 58);
		CMCE A 1 Offset(11, 52);
		CMCE A 1 Offset(8, 46);
		CMCE A 1 Offset(5, 40);
		CMCE A 1 Offset(3, 36);
		CMCE A 1 Offset(2, 33);
		Goto Ready;
	}
	
	action void MaceAttack() {
		if (player == null) return;
		
		Actor victim, puff;
		[victim, puff] = MeleeAttack(RandomizeDamage(MACE_DAMAGE), MELEERANGE_LONG, 0, 6, "cMacePuff");
		
		if (victim || puff) {
			WeaponShake(2, 6);
			A_AlertMonsters(ALERT_DIST_NORM);
		} else {
			A_AlertMonsters(ALERT_DIST_NEAR);
		}
	}
}

class cMacePuff : cPuff {
	Default {
		RenderStyle "Translucent";
		Alpha 0.6;
		+FORCEXYBILLBOARD
		SeeSound "FighterAxeHitThing";
		AttackSound "FighterHammerHitWall";
		ActiveSound "FighterHammerMiss";
	}
	
	States {
	Spawn:
		FHFX ST 4;
		FHFX UVW 4 A_FadeOut(0.2);
		Stop;
	HitBleeding:
		TNT1 A 0;
		Stop;
	HitInvulnerable:
	HitWall:
		TNT1 A 0 A_SetRenderStyle(0.7, STYLE_Add);
		MHIT A 3 SparkParticles();
		MHIT AAAA 2 A_FadeOut(0.2);
		Stop;
	}
}
