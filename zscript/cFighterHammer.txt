class cHammer : cWeapon replaces FWeapHammer {
	Default {
		Inventory.RestrictedTo "cFighterPlayer";
		Weapon.AmmoType1 "Mana2";
		Weapon.AmmoUse1 5;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "Mana2";
		Weapon.AmmoUse2 4;
		Weapon.AmmoGive2 0;
		+WEAPON.AMMO_OPTIONAL
		Weapon.YAdjust -10;
		Weapon.BobStyle "Smooth";
		Weapon.BobSpeed 1.4;
		Weapon.BobRangeX 1.0;
		Weapon.BobRangeY 1.0;
		Weapon.SelectionOrder 900;
		+WEAPON.MELEEWEAPON
		Inventory.PickupMessage "$TXT_WEAPON_F3";
		Obituary "$OB_MPFWEAPHAMMERM";
		Tag "$TAG_FWEAPHAMMER";
	}
	
	bool empowered;
	
	States {
	Spawn:
		WFHM A -1;
		Stop;
	Select:
		TNT1 A 0 {
			A_SetCrosshair(52);
			invoker.empowered = false;
		}
		FHMR A 1 A_Raise(SELECT_SPEED);
		Wait;
	Deselect:
		FHMR A 1 A_Lower(SELECT_SPEED);
		Wait;
	Deselect.Empowered:
		FHMR FFFFGGGGHHHH 1 A_Lower(SELECT_SPEED);
		Wait;
	Ready:
		TNT1 A 0 A_JumpIf(invoker.empowered, "Ready.Empowered");
		FHMR A 1 A_WeaponReady();
		Loop;
	Ready.Empowered:
		TNT1 A 0 A_JumpIf(!invoker.empowered, "Ready");
		FHMR FFFFGGGGHHHH 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Fire:
		FHMR B 2 Offset(14, 38);
		FHMR B 2 Offset(5, 30);
		FHMR B 2 Offset(-7, 24);
		FHMR B 3 Offset(-16, 23);
		FHMR B 1 Offset(-21, 25);
		FHMR C 2 Offset(10, 30);
		FHMR C 2 Offset(-7, 48);
		FHMR C 1 Offset(-21, 69) HammerAttack();
		FHMR D 1 Offset(17, 30) HammerThrow();
		FHMR D 1 Offset(6, 48);
		FHMR E 1 Offset(24, 30);
		FHMR E 1 Offset(14, 47);
		FHMR E 1 Offset(2, 72);
		TNT1 A 16;
		Goto WindDown;
	Fire.Empowered:
		FHMR I 2 Offset(14, 38);
		FHMR I 2 Offset(5, 30);
		FHMR I 2 Offset(-7, 24);
		FHMR I 3 Offset(-16, 23);
		FHMR I 1 Offset(-21, 25);
		FHMR J 2 Offset(10, 30);
		FHMR J 2 Offset(-7, 48);
		FHMR J 1 Offset(-21, 69) HammerAttack();
		FHMR K 1 Offset(14, 30) HammerThrow();
		FHMR K 1 Offset(3, 48);
		FHMR L 1 Offset(24, 30);
		FHMR L 1 Offset(14, 47);
		FHMR L 1 Offset(2, 72);
		TNT1 A 16;
		Goto WindDown;
	WindDown:
		TNT1 A 0 A_JumpIf(invoker.empowered, "WindDown.Empowered");
		FHMR A 2 Offset(11, 80);
		FHMR A 2 Offset(7, 68);
		FHMR A 1 Offset(4, 56);
		FHMR A 1 Offset(2, 44);
		FHMR A 1 Offset(2, 34);
		Goto Ready;
	WindDown.Empowered:
		FHMR G 2 Offset(11, 80);
		FHMR G 2 Offset(7, 68);
		FHMR H 1 Offset(4, 56);
		FHMR H 1 Offset(2, 44);
		FHMR H 1 Offset(2, 34);
		Goto Ready;
	AltFire:
		TNT1 A 0 {
			A_PlaySound("FighterHammerEmpower", CHAN_WEAPON);
			if (invoker.DepleteAmmo(true, true)) {
				invoker.empowered = true;
			}
		}
		Goto Ready;
	}
	
	const HAMMER_DAMAGE = 70;
	const HAMMER_POWER_DAMAGE = 110;
	
	action void HammerAttack() {
		if (player == null) return;
		
		WeaponShake(2, 6);
		
		Actor victim, puff;
		if (invoker.empowered) {
			[victim, puff] = MeleeAttack(RandomizeDamage(HAMMER_POWER_DAMAGE), MELEERANGE_MED, 6, 15, "cHammerPowerPuff", 'Fire');
			
			if (puff) {
				puff.RadiusAttack(self, 110, 128, 'Fire', 0, 32);
				invoker.empowered = false;
			}
			
			WeaponShake(2, 14);
			
			// Cause pain
			if (victim && !(victim.bINVULNERABLE || victim.bBOSS)) victim.TriggerPainChance('Melee', true);
		} else {
			[victim, puff] = MeleeAttack(RandomizeDamage(HAMMER_DAMAGE), MELEERANGE_SHORT, 6, 15, "cHammerPuff");
		}
		
		if (victim || puff) {
			A_AlertMonsters(ALERT_DIST_NORM);
			weaponSpecial = false; // Don't throw a hammer
		} else {
			A_AlertMonsters(ALERT_DIST_NEAR);
			weaponSpecial = true; // Prepare to throw a hammer
		}
	}
	
	action void HammerThrow() {
		if (player == null) return;
		
		if (!weaponSpecial) return;
		
		Weapon weap = player.readyWeapon;
		if (weap != null) {
			if (!weap.CheckAmmo(weap.bAltFire ? weap.altFire : weap.primaryFire, false, true)) return;
			if (!weap.DepleteAmmo(weap.bAltFire, false)) return;
		} else {
			return;
		}
		
		A_FireProjectile("cHammerMissile", 0, false, 0, 0);
	}
	
	override State GetDownState() {
		return empowered ? FindState("Deselect.Empowered") : Super.GetDownState();
	}
	
	override State GetReadyState() {
		return empowered ? FindState("Ready.Empowered") : Super.GetReadyState();
	}
	
	override State GetAtkState(bool hold) {
		return empowered ? FindState("Fire.Empowered") : Super.GetAtkState(hold);
	}
}

class cHammerMissile : HammerMissile {
	cFlare flare;
	
	const BLAST_DAMAGE = 80.0;
	const NOBLAST_DAMAGE = 45.0;
	
	Default {
		Radius 10;
		Height 14;
		DamageFunction cWeapon.RandomizeDamage(45);
		Speed 30;
		Decal "Scorch";
	}
	
	States {
	Spawn:
		FHFX A 2 Bright Light("ThrownHammer");
		FHFX B 2 Bright Light("ThrownHammer") A_PlaySound("FighterHammerContinuous");
		FHFX CDEFGH 2 Bright Light("ThrownHammer");
		Loop;
	Death:
		TNT1 A 14 {
			if (flare) flare.Destroy();
			A_Explode(cWeapon.RandomizeDamage(BLAST_DAMAGE), 128, XF_NOSPLASH);
			A_AlertMonsters(ALERT_DIST_NORM);
			A_SpawnItemEx("cHammerBlast");
			A_Quake(2, 14, 0, 120, "");
		}
		Stop;
	}
	
	override int SpecialMissileHit(Actor victim) {
		int r = Super.SpecialMissileHit(victim);
		if (r == -1 && victim && victim.bNORADIUSDMG) {
			victim.DamageMobj(self, target, NOBLAST_DAMAGE, damageType);
		}
		return r;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		flare = cFlare(Spawn("cFlareRed")).Init(self, 0, 32, 0.2);
	}
}

class cHammerBlast : Actor {
	Default {
		+NOINTERACTION
		+NOBLOCKMAP
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		FHFX IJKL 3 Bright Light("HammerBlast1");
		FHFX MNO 3 Bright Light("HammerBlast2");
		FHFX PQR 3 Bright Light("HammerBlast3");
		Stop;
	}
}

// Puff
class cHammerPuff : cPuff {
	Default {
		RenderStyle "Translucent";
		Alpha 0.5;		
		+FORCEXYBILLBOARD
		SeeSound "FighterHammerHitThing";
		AttackSound "FighterHammerHitWall";
		ActiveSound "FighterHammerMiss";
	}
	
	States {
	Spawn:
		FHFX ST 3;
		FHFX UVW 4 A_FadeOut(0.1);
		Stop;
	}
}

class cHammerPowerPuff : cPuff {
	Default {
		RenderStyle "Add";
		+FORCEXYBILLBOARD
		SeeSound "FighterHammerPowerHit";
		AttackSound "FighterHammerPowerHit";
		ActiveSound "FighterHammerMiss";
	}
	
	States {
	Spawn:
		XPL1 ABCDEF 3 Bright;
		Stop;
	}
}
