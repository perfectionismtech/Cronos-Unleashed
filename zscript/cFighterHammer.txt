class cHammer : cWeapon replaces FWeapHammer {
	Default {
		Inventory.RestrictedTo "cFighterPlayer";
		Weapon.AmmoType1 "Mana2";
		Weapon.AmmoUse1 5;
		Weapon.AmmoGive1 0;
		+WEAPON.AMMO_OPTIONAL
		Weapon.YAdjust -10;
		Weapon.BobStyle "Smooth";
		Weapon.BobSpeed 1.4;
		Weapon.BobRangeX 1.0;
		Weapon.BobRangeY 1.0;
		Weapon.SelectionOrder 900;
		+WEAPON.MELEEWEAPON
		Inventory.PickupMessage "$TXT_WEAPON_F3";
		Obituary "$OB_MPFWEAPHAMMERM";
		Tag "$TAG_FWEAPHAMMER";
	}
	
	const HAMMER_DAMAGE = 70;
	
	States {
	Spawn:
		WFHM A -1;
		Stop;
	Select:
		TNT1 A 0 A_SetCrosshair(52);
		FHMR A 1 A_Raise(SELECT_SPEED);
		Wait;
	Deselect:
		FHMR A 1 A_Lower(SELECT_SPEED);
		Wait;
	Ready:
		FHMR A 1 A_WeaponReady();
		Loop;
	Fire:
		FHMR B 2 Offset(14, 38);
		FHMR B 2 Offset(5, 30);
		FHMR B 2 Offset(-7, 24);
		FHMR B 3 Offset(-16, 23);
		FHMR B 1 Offset(-21, 25);
		FHMR C 2 Offset(10, 30);
		FHMR C 2 Offset(-7, 48);
		FHMR C 1 Offset(-21, 69) HammerAttack();
		FHMR D 1 Offset(17, 30) HammerThrow();
		FHMR D 1 Offset(6, 48);
		FHMR E 1 Offset(24, 30);
		FHMR E 1 Offset(14, 47);
		FHMR E 1 Offset(2, 72);
		TNT1 A 16;
		Goto WindDown;
	WindDown:
		FHMR A 2 Offset(11, 80);
		FHMR A 2 Offset(7, 68);
		FHMR A 1 Offset(4, 56);
		FHMR A 1 Offset(2, 44);
		FHMR A 1 Offset(2, 34);
		Goto Ready;
	}
	
	action void HammerAttack() {
		if (player == null) return;
		
		WeaponShake(2, 6);
		
		Actor victim, puff;
		[victim, puff] = MeleeAttack(RandomizeDamage(HAMMER_DAMAGE), MELEERANGE_SHORT, 6, 12, "cHammerPuff");
		
		if (victim || puff) {
			A_AlertMonsters(ALERT_DIST_NORM);
			weaponSpecial = false; // Don't throw a hammer
		} else {
			A_AlertMonsters(ALERT_DIST_NEAR);
			weaponSpecial = true; // Prepare to throw a hammer
		}
	}
	
	action void HammerThrow() {
		if (player == null) return;
		
		if (!weaponSpecial) return;
		
		Weapon weap = player.readyWeapon;
		if (weap != null) {
			if (!weap.CheckAmmo(weap.bAltFire ? weap.altFire : weap.primaryFire, false, true)) return;
			if (!weap.DepleteAmmo(weap.bAltFire, false)) return;
		} else {
			return;
		}
		
		A_FireProjectile("cHammerMissile", 0, false, 0, 0);
	}
}

class cHammerMissile : HammerMissile {
	cFlare flare;
	
	const BLAST_DAMAGE = 80.0;
	const NOBLAST_DAMAGE = 45.0;
	
	Default {
		Radius 10;
		Height 14;
		DamageFunction cWeapon.RandomizeDamage(45);
		Speed 30;
		Decal "Scorch";
	}
	
	States {
	Spawn:
		FHFX A 2 Bright Light("ThrownHammer");
		FHFX B 2 Bright Light("ThrownHammer") A_PlaySound("FighterHammerContinuous");
		FHFX CDEFGH 2 Bright Light("ThrownHammer");
		Loop;
	Death:
		TNT1 A 14 {
			if (flare) flare.Destroy();
			A_Explode(cWeapon.RandomizeDamage(BLAST_DAMAGE), 128, XF_NOSPLASH);
			A_AlertMonsters(ALERT_DIST_NORM);
			A_SpawnItemEx("cHammerBlast");
			A_Quake(2, 14, 0, 120, "");
		}
		Stop;
	}
	
	override int SpecialMissileHit(Actor victim) {
		int r = Super.SpecialMissileHit(victim);
		if (r == -1 && victim && victim.bNORADIUSDMG) {
			victim.DamageMobj(self, target, NOBLAST_DAMAGE, damageType);
		}
		return r;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		flare = cFlare(Spawn("cFlareRed")).Init(self, 0, 32, 0.2);
	}
}

class cHammerBlast : Actor {
	Default {
		+NOINTERACTION
		+NOBLOCKMAP
		+FORCEXYBILLBOARD
	}
	
	States {
	Spawn:
		FHFX IJKL 3 Bright Light("HammerBlast1");
		FHFX MNO 3 Bright Light("HammerBlast2");
		FHFX PQR 3 Bright Light("HammerBlast3");
		Stop;
	}
}

// Puff
class cHammerPuff : cPuff {
	Default {
		RenderStyle "Translucent";
		Alpha 0.5;		
		+FORCEXYBILLBOARD
		SeeSound "FighterHammerHitThing";
		AttackSound "FighterHammerHitWall";
		ActiveSound "FighterHammerMiss";
	}
	
	States {
	Spawn:
		FHFX ST 3;
		FHFX UVW 4 A_FadeOut(0.1);
		Stop;
	}
}
